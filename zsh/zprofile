#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Homebrew
#
if [[ "$OSTYPE" == darwin* ]]; then
  # Apple Silicon Mac
  if [[ -f "/opt/homebrew/bin/brew" ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv zsh)"
  # Intel Mac
  elif [[ -f "/usr/local/bin/brew" ]]; then
    eval "$(/usr/local/bin/brew shellenv zsh)"
  fi
elif [[ "$OSTYPE" == linux* ]]; then
  if [[ -f "/home/linuxbrew/.linuxbrew/bin/brew" ]]; then
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv zsh)"
  elif [[ -f "$HOME/.linuxbrew/bin/brew" ]]; then
    eval "$($HOME/.linuxbrew/bin/brew shellenv zsh)"
  fi
fi

#
# Browser
#

if [[ -z "$BROWSER" ]]; then
  if [[ "$OSTYPE" == darwin* ]]; then
    export BROWSER='open'
  elif [[ "$OSTYPE" == linux* ]]; then
    export BROWSER='xdg-open'
  fi
fi

#
# Editors
#

if [[ -z "$EDITOR" ]]; then
  export EDITOR='nvim'
fi
if [[ -z "$VISUAL" ]]; then
  export VISUAL='nvim'
fi
if [[ -z "$PAGER" ]]; then
  export PAGER='less'
fi

# nvim でmanページを表示
export MANPAGER='nvim +Man!'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  $HOME/{,s}bin(N)
  /opt/{homebrew,local}/{,s}bin(N)
  /home/linuxbrew/.linuxbrew/{,s}bin(N)
  $HOME/.linuxbrew/{,s}bin(N)
  /usr/local/{,s}bin(N)
  $path
)

#
# GitHub Token (mise の GitHub API レート制限回避)
# gh CLI でログイン済みの場合はトークンを自動取得する
#

if [[ -z "$GITHUB_TOKEN" ]] && command -v gh &>/dev/null; then
  if gh auth status &>/dev/null 2>&1; then
    export GITHUB_TOKEN="$(gh auth token 2>/dev/null)"
  fi
fi

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X to enable it.
if [[ -z "$LESS" ]]; then
  export LESS='-g -i -M -R -S -w -X -z-4'
fi

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if [[ -z "$LESSOPEN" ]] && (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi
